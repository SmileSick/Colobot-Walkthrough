extern void Earth3_0_1_12()
{
	message("Иди на корабль, пока я здесь со всем разберусь.");
	Research(ResearchTracked);
	BuildBot(TrackedGrabber, "Bbox");
	object item = radar(SpaceShip);
	goto(item.position);
}

void Research(int res)
{
	BringTo(PowerCell, ResearchCenter);
	object item = radar(ResearchCenter);
	item.research(res);
}

void BuildBot(int cat, string prog)
{
	BringTo(Titanium, BotFactory);
	move(-7);
	object item = radar(BotFactory);
	item.factory(cat, prog);
	BringTo(PowerCell, cat);
	move(-7);
}

void GrabCat(int cat)
{
	object item = radar(cat);
	goto(item.position);
	turn(direction(item.position));
	motor(1, 1);
	wait(0.5);
	grab();
}

void BringTo(int cat, int target)
{
	GrabCat(cat);
	while(radar(target) == null) wait(1);
	object item = radar(target);
	goto(item.position);
	drop();
}

public void object::Bbox()
{
	while(this.energyCell == null) wait(1);
	while(radar(Any, 180, 30, 1, 7) != null) wait(1);
	move(-7);
	BringTo(BlackBox, SpaceShip);
	object ship = radar(SpaceShip);
	object human = radar(Me);
	while(distance(human.position, ship.position) > 10)
	{
		message("Ты где там шаришься? Живо на борт.");
		wait(rand() + 2);
	}
	ship.takeoff();
}
