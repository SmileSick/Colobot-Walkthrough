extern void M11Plus()
{
	message("Иди на корабль, пока я здесь со всем разберусь.");
	Research(ResearchTracked);
	Research(ResearchBuilder);
	BuildBot(TrackedBuilder, "Info");
	ErrGoto(radar(SpaceShip).position);
}

void object::Research(int res)
{
	if(researched(res)) return;
	BringTo(PowerCell, ResearchCenter);
	research(res);
	while(!researched(res)) wait(1);
	grab();
	ErrGoto(flatspace(this.position, 2));
	drop();
}

void BuildBot(int cat, string prog)
{
	BringTo(Titanium, BotFactory);
	move(-3);
	factory(cat, prog);
	BringTo(PowerCell, cat);
}

void GrabCat(int cat)
{
	object item;
	if(cat == PowerCell) item = FindPower();
	else item = radar(cat);
	ErrGoto(item.position);
	motor(1, 1);
	wait(0.5);
	grab();
}

void BringTo(int cat, int target)
{
	GrabCat(cat);
	object item;
	while((item = radar(target)) == null) wait(1);
	ErrGoto(item.position);
	drop();
}

object FindPower()
{
	object[] cells = radarall(PowerCell);
	int len = sizeof(cells);
	for(int i = 0; i < len; i++)
	{
		if(cells[i].energyLevel == 1) return cells[i];
	}
	return null;
}

public void object::Info()
{
	while(this.energyCell == null) wait(1);
	while(radar(Any, 180, 30, 1, 7) != null) wait(1);
	object item = radar(BlueFlag);
	item = search(Titanium, item.position);
	ErrGoto(item.position);
	build(ExchangePost);
	send("", 0);
	item = radar(SpaceShip);
	ErrGoto(item.position);
	while(radar(BlackBox) == null) wait(1);
	while(search(TrackedGrabber, item.position, 0, 20) != null) wait(1);
	while(search(Me, item.position, 0, 6) == null)
	{
		message("Ты где там шаришься? Живо на борт.");
		wait(1 + rand()*2);
	}
	takeoff();
}

void ErrGoto(point pos)
{
	errmode(0);
	while(goto(pos) != 0) wait(1);
	errmode(1);
}
