extern void object::Mission21()
{
    Research();
    BuildBot();
    Charge();
}
 
void object::Research()
{
    Build(ResearchCenter);
    Power(ResearchCenter);
    object item = radar(ResearchCenter);
    item.research(ResearchWinged);
}
 
void object::BuildBot()
{
    Build(BotFactory);
    object item = radar(Titanium);
    goto(item.position);
    grab();
    item = radar(BotFactory);
    goto(item.position);
    drop();
    move(-5);
    item.factory(WingedGrabber, "GiveOre");
}
 
void object::Charge()
{
    Power(WingedGrabber);
    object item = radar(SpaceShip);
    goto(item.position);
}
 
void object::Power(int cat)
{
    object item = radar(PowerCell);
    goto(item.position);
    grab();
    while(radar(cat)==null) wait(1);
    item = radar(cat);
    goto(item.position);
    drop();
    move(-5);
}
 
void object::Build(int cat)
{
    object item = radar(Titanium);
    goto(item.position);
    grab();
    goto(space(this.position,15,100,8));
    move(-1.5);
    drop();
    build(cat);
}
 
public void object::GiveOre()
{
    object item;
    while(this.energyCell == null) wait(1);
    while(radar(WheeledGrabber, 0, 360, 0, 10) != null) wait(1);
    move(-5);
    for(int i = -1; i<3; i++)
    {
        item = radar(TitaniumOre, 0, 360, 100);
        goto(item.position);
        grab();
        item = radar(SpaceShip);
        goto(item.position);
        turn(i*45);
        drop();
        if(i == 2) return;
        turn(-i*45);
        move(-5);
    }
}
