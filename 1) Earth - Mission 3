extern void object::Mission13()
{
    Research();
    BuildBot();
    Charge();
}
void object::Research()
{
    message("Иди на корабль, пока я здесь со всем разберусь.");
    Power(ResearchCenter);
    object item = radar(ResearchCenter);
    item.research(ResearchTracked);
}
void object::BuildBot()
{
    object item = radar(Titanium);
    goto(item.position);
    grab();
    item = radar(BotFactory);
    goto(item.position);
    drop();
    move(-5);
    item.factory(TrackedGrabber, "TrackedGrab");
}
void object::Charge()
{
    Power(TrackedGrabber);
    move(-5);
    object item = radar(SpaceShip);
    goto(item.position);
}
void object::Power(int cat)
{
    object item = radar(PowerCell);
    goto(item.position);
    grab();
    while(radar(cat)==null) wait(1);
    item = radar(cat);
    goto(item.position);
    drop();
}
public void object::TrackedGrab()
{
    object astro = radar(Me);
    object ship = radar(SpaceShip);
    while(this.energyCell==null) wait(1);
    while(radar(WheeledGrabber,180,180,0,7)!=null) wait(1);
    move(-5);
    object item = radar(BlackBox);
    goto(item.position);
    grab();
    goto(ship.position);
    drop();
    while(distance(astro.position, ship.position)>10)
    {
        message("Ты где там шаришься? Живо на борт.");
        wait(2);
    }
    ship.takeoff();
}
